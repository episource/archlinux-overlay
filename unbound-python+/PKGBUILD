# Maintainer: Philipp Serr <episource@gmx.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Hisato Tatekura <hisato_tatekura@excentrics.net>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>


distname=unbound
pkgname=$distname-python+
pkgver=1.7.0
pkgrel=1
pkgdesc='Validating, recursive, and caching DNS resolver with python scripting and custom patches.'
url='https://unbound.net/'
license=('custom:BSD')
arch=('x86_64')
makedepends=('expat' 'swig' 'flex')
optdepends=('expat: unbound-anchor')
depends=('openssl' 'ldns' 'libevent' 'fstrm' 'protobuf-c' 'dnssec-anchors' 'python2')
provides=("$distname=$pkgver" "$distname-python=$pkgver")
conflicts=("$distname" "$distname-python")
backup=('etc/unbound/unbound.conf')
source=("$distname::git+https://github.com/episource/unbound#branch=dev/all-merged/v${pkgver}"
        'sysusers.d'
        'tmpfiles.d'
        'service'
        'hook'
        'conf')
sha256sums=('SKIP'
            '85b8f00881fb93bcce83bdfe3246463a396eb5b352c0d7f5fca32fcb839d22fa'
            '31a573f43287dd7e3678be1680388bfc7d8dee8280eb2443f521a4b349aaa6b6'
            '526ee18a5394073404ee10a67addf9894efa43fa234234562e84a1641d54b556'
            '2746aede36b1f57efdcc370b7643ce31ff9e6acb9a1f62705987b07eaed866a3'
            '07d764f4f42adfba9685c5c7feef81116118f4a1772b118aa3ebbe059e8a513e')

prepare() {
    cd "${srcdir}/${distname}"
    rev_date=$( git log -1 --format="%ad" --date=format:"%b %e, %Y" )
    
    files=( 
        "doc/README" "doc/unbound.8.in" "doc/unbound.conf.5.in"
        "doc/unbound-checkconf.8.in" "doc/unbound-control.8.in"
        "doc/unbound-anchor.8.in" "doc/unbound-host.1.in"
        "doc/example.conf.in" "doc/libunbound.3.in" 
    )
    
    for f in "${files[@]}"; do
        sed -i "s/@version@/$pkgver/g;s/@date@/$rev_date/g" "$f"
    done
}

build() {
    cd "${srcdir}/${distname}"
    PYTHON_VERSION=2.7 ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--disable-rpath \
		--enable-dnstap \
		--enable-pie \
		--enable-relro-now \
		--with-conf-file=/etc/unbound/unbound.conf \
		--with-pidfile=/run/unbound.pid \
		--with-rootkey-file=/etc/trusted-key.key \
		--with-libevent \
		--with-pythonmodule \

	make
}

package() {
	cd "${srcdir}/${distname}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 doc/example.conf.in "${pkgdir}/etc/unbound/unbound.conf.example"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../sysusers.d "${pkgdir}/usr/lib/sysusers.d/unbound.conf"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/unbound.service"
	install -Dm644 ../conf "${pkgdir}/etc/unbound/unbound.conf"

	# Trust anchor file available from within unbound's chroot.
	install -Dm644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/unbound.conf"
	install -Dm644 ../hook "${pkgdir}/usr/share/libalpm/hooks/unbound-key.hook"
}
