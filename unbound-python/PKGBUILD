# Maintainer: Philipp Serr <episource@gmx.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Hisato Tatekura <hisato_tatekura@excentrics.net>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

distname=unbound
pkgname=$distname-python
pkgver=1.14.0
pkgrel=2
makedepends=(expat protobuf-c systemd python swig)
pkgdesc="Validating, recursive, and caching DNS resolver with python scripting."
arch=(x86_64)
url="https://unbound.net/"
license=(BSD)
depends=(dnssec-anchors fstrm hiredis ldns libevent libnghttp2 libsodium openssl python)
makedepends=(expat protobuf-c python swig systemd)
optdepends=(
  'expat: for unbound-anchor'
  'sh: for unbound-control-setup'
  'python: for python-bindings'
)
provides=(libunbound.so)
backup=(etc/${distname}/${distname}.conf)
source=(
  "https://unbound.net/downloads/${distname}-${pkgver}.tar.gz"{,.asc}
  "${distname}-1.14.0-trust_anchor_file.patch"
  ${distname}-sysusers.conf
  ${distname}-tmpfiles.conf
  ${distname}-trusted-key.hook
)
sha512sums=('57f91d898b0a5d42e6a2ff1ccaec474f04dd5ad3c98e7eb7aa8d5eaa23b587f3077cf7eddf4df38f537c6d387028f12c2518ff13b7249aa7a1155cd6532a46b5'
            'SKIP'
            '9590d3d459d96f99cbc7482fae0f5318dd22a034e45cff18079e4f3c9f9c3c1d7af90cdd5353fb469eac08c535555fd164097b496286b807b2117e8a3a6cd304'
            'ef71d4e9b0eb0cc602d66bd0573d9424578fe33ef28a852c582d56f0fd34fdd63046c365ef7aed8b84a461b81254240af7ad3fd539da72f9587817d21bd6c585'
            '6b1849ae9d7cf427f6fa6cd0590e8f8c3f06210d2d6795e543b0f325a9e866db0f5db2275a29fa90f688783c0dd16f19c8a49a9817d5f5444e13f8f2df3ff712'
            '613826cdf5ab6e77f2805fa2aa65272508dcd11090add1961b3df6dfac3b67db016bc9f45fbcf0ef0de82b2d602c153d5263a488027a6cf13a72680b581b266d')
b2sums=('ceccfbcbc16370153005f32d19be44bd41a4ac935cb7b9a90128d604c9874b06cc7de8b92dc34585d7957f0ad90ce2f441c2ed092c9307bcf4335f8d99eb8bd3'
        'SKIP'
        '0978ab5c0474ed29de9c0904a46d114413e094dafeadaac4f10cdbc19e4152fcc064d7cdb8c331da7c2531075aa699326b84e21da1a8218a6f00a10f0e107b3d'
        '292a3c2e5fde292a03b6c9b2ddabd5089f52e73b50a404c3d9f54c1a43184924b661a21eea61cc521c594c1005a3b40b630fa585a38195c61298f9b24b248b92'
        'd3951006b43068be904c6b91a9e0563d56228225854e12b40abbdd4ba9b47338e97265837297a6de879acbc8051bb749163f9457683f5e12fc29ac2e7b687fd3'
        'd28785390eb6c125bd26ca11f097fe8864b080482157deeb7c70e9bee47ff2844abaed574db59a7c152ed3ec0acba05cfee4c3751f7a9f553320b064578f86c7')
validpgpkeys=(EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D) # W.C.A. Wijngaards <wouter@nlnetlabs.nl>

prepare() {
  cd "${distname}-${pkgver}"
  # enable trusted-anchor-file and set it to an unbound specific location
  patch -Np1 -i ../"${distname}-1.14.0-trust_anchor_file.patch"
  autoreconf -fiv
}

build() {
    cd "${distname}-${pkgver}"
    PYTHON_VERSION=3 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --disable-rpath \
        --enable-dnscrypt \
        --enable-dnstap \
        --enable-pie \
        --enable-relro-now \
        --enable-subnet \
        --enable-systemd \
        --enable-tfo-client \
        --enable-tfo-server \
        --enable-cachedb \
        --with-libhiredis \
        --with-conf-file=/etc/unbound/unbound.conf \
        --with-pidfile=/run/unbound.pid \
        --with-rootkey-file=/etc/trusted-key.key \
        --with-libevent \
        --with-libnghttp2 \
        --with-pyunbound \
        --with-pythonmodule
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${distname}-${pkgver}"
  make -k check
}

package() {
  depends+=(libprotobuf-c.so libsystemd.so)

  cd "${distname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 contrib/${distname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -vDm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${distname}/
  install -vDm 644 ../${distname}-sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/${distname}.conf
  install -vDm 644 ../${distname}-tmpfiles.conf "${pkgdir}"/usr/lib/tmpfiles.d/${distname}.conf
  # libalpm hook to copy the dnssec-anchors provided key to /etc/unbound
  install -vDm 644 ../${distname}-trusted-key.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}
